// Timetable time and minutes
?start          : train_full_id | timing_point | ttime | tmin | allowances | activity

// Timetable (one train)
timetable       : train_full_id "\n" train_desc "\n" (timing_point "\n")+
train_desc      : ttime TT_ORIGIN "-" TT_DEST "(" TC_NAME ")"
TT_ORIGIN       : ALPHANUMERIC+
TT_DEST         : ALPHANUMERIC+

// Train category
train_category  : TC_NAME "\n" DIGITS "m," DIGITS "mph," POWER_TYPE ("," POWER_TYPE)*
TC_NAME         : ALPHANUMERIC+
POWER_TYPE      : (ALPHANUMERIC | "_")+

// Train ID
train_full_id   : train_id ["/" train_uid]
train_uid       : ALPHANUMERIC+
?train_id       : BR_TRAIN_ID | LU_TRAIN_ID | MISC_TRAIN_ID

BR_TRAIN_ID.1   : DIGIT LETTER DIGIT DIGIT
LU_TRAIN_ID.1   : LETTER DIGIT DIGIT DIGIT
MISC_TRAIN_ID   : ALPHANUMERIC~4

// Timing point
timing_point    : location ttime [allowances] [activities]

location        : TIPLOC ["." PLATFORM]
TIPLOC          : LETTER ALPHANUMERIC*
PLATFORM        : ALPHANUMERIC+

// Times
ttime           : HOUR stopmode tmin
tmin.2          : MINUTE [HALFMINUTE]

HOUR            : ("0".."2")? DIGIT
MINUTE          : ("0".."5")? DIGIT
HALFMINUTE      : "h" | "H" | "Â½"

stopmode:       STOPPING | PASSING | SET_DOWN | IF_REQUIRED
                | REQUEST_STOP | DWELL_TIME | THROUGH_LINE

STOPPING        : ":"
PASSING         : "/"
SET_DOWN        : "d"
IF_REQUIRED     : "*"
REQUEST_STOP    : "r"
DWELL_TIME      : "w"
THROUGH_LINE    : "t"

// Allowances
allowances      : (eng_allowance | path_allowance | perf_allowance)+

eng_allowance   : "[" tmin "]"
path_allowance  : "(" tmin ")"
perf_allowance  : "<" tmin ">"

// Activity
activities          : activity+
activity            : activity_type ":" train_full_id

activity_type:      NEXT | JOIN
                    | DIVIDE_REAR | DIVIDE_FRONT
                    | DETACH_ENGINE_REAR | DETACH_ENGINE_FRONT
                    | DROP_COACHES_REAR | DROP_COACHES_FRONT
                    | PLATFORM_SHARE

NEXT                : "N"i
JOIN                : "J"i
DIVIDE_REAR         : "DR"i
DIVIDE_FRONT        : "DF"i
DETACH_ENGINE_REAR  : "DER"i
DETACH_ENGINE_FRONT : "DEF"i
DROP_COACHES_REAR   : "DCR"i
DROP_COACHES_FRONT  : "DCF"i
PLATFORM_SHARE      : "PS"i

ALPHANUMERIC        : DIGIT | LETTER
DIGITS              : DIGIT+

%import common (DIGIT, LETTER, WS_INLINE)
%ignore WS_INLINE | "\r" | "\f"
